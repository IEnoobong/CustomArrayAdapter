apply plugin: 'com.android.library'

apply plugin: 'kotlin-android'

apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'org.jetbrains.dokka-android'

android {
    compileSdkVersion compile_sdk_version

    defaultConfig {
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/javadoc"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation "com.android.support:support-annotations:$support_version"

    testImplementation "junit:junit:4.12"
}

ext {
    pomPackageName = 'customarrayadapter'
    pomPackageDescription = 'Simple Android Kotlin Library to help create custom array adapters for adapter views like spinners'
    pomPackageLicenses = ["Apache-2.0"]
    pomVersion = lib_version
    pomGroupId = 'co.enoobong'
    // pomArtifactId, pomArtifactName, pomArtifactDescription

    pomSiteUrl = 'https://github.com/IEnoobong/CustomArrayAdapter'
    pomGitUrl = 'https://github.com/IEnoobong/CustomArrayAdapter.git'

    pomLicenses = {
        license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
        }
    }

    pomDevelopers = {
        developer {
            id 'ienoobong'
            name 'Ibanga Enoobong'
            email 'ibangaenoobong@yahoo.com'
        }
    }

    bintrayRepo = 'CustomArrayAdapter'
    bintrayName = 'CustomArrayAdapter'

    publishedGroupId = 'co.enoobong'
    libraryName = ' CustomArrayAdapter'
    artifact = 'customarrayadapter'

    libraryDescription = 'Simple Android Kotlin Library to help create custom array adapters for adapter views like spinners'

    siteUrl = 'https://github.com/IEnoobong/CustomArrayAdapter'
    gitUrl = 'https://github.com/IEnoobong/CustomArrayAdapter.git'

    libraryVersion = lib_version

    developerId = 'ienoobong'
    developerName = 'Ibanga Enoobong Ime'
    developerEmail = 'ibangaenoobong@yahoo.com'

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

group = publishedGroupId
version = libraryVersion

install {
    repositories.mavenInstaller {
        pom.project {
            packaging 'aar'
            groupId publishedGroupId
            artifactId artifact

            name libraryName
            description libraryDescription
            url siteUrl

            licenses {
                license {
                    name licenseName
                    url licenseUrl
                }
            }
            developers {
                developer {
                    id developerId
                    name developerName
                    email developerEmail
                }
            }
            scm {
                connection gitUrl
                developerConnection gitUrl
                url siteUrl
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    excludes = ['**/*.kt']
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = bintrayRepo
        name = bintrayName
        desc = libraryDescription
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = allLicenses
        dryRun = false
        publish = true
        override = false
        publicDownloadNumbers = true
        version {
            desc = libraryDescription
        }
    }
}
